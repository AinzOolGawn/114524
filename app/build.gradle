plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def appVersionName = "1.1.0"
def appVersionCode = 7

android {
    compileSdk 33
    buildToolsVersion = "33.0.2"

    defaultConfig {
        applicationId "me.rhunk.snapenhance"
        minSdk 28
        //noinspection OldTargetApi
        targetSdk 33
        versionCode appVersionCode
        versionName appVersionName
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "app-${appVersionName}-${variant.flavorName}.apk"
        }
    }

    flavorDimensions += "release"

    productFlavors {
        armv8 {
            isDefault = true
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
            dimension "release"
        }
        armv7 {
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a"
            }
            packagingOptions {
                exclude 'lib/armeabi-v7a/*_neon.so'
            }
            dimension "release"
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'me.rhunk.snapenhance'
}

afterEvaluate {
    //auto install for debug purpose
    tasks.named("assembleArmv8Debug").get().doLast {
        def apkDebugFile = android.applicationVariants.find { it.buildType.name == "debug" && it.flavorName == "armv8" }.outputs[0].outputFile
        try {
            println "Killing Snapchat"
            exec {
                commandLine "adb", "shell", "am", "force-stop", "com.snapchat.android"
            }
            println "Installing debug build"
            exec() {
                commandLine "adb", "install", "-r", "-d", apkDebugFile.absolutePath
            }
            println "Starting Snapchat"
            exec {
                commandLine "adb", "shell", "am", "start", "com.snapchat.android"
            }
        } catch (Throwable t) {
            println "Failed to install debug build"
            t.printStackTrace()
        }
    }
}

tasks.register('getVersion') {
    doLast {
        def version = new File('app/build/version.txt')
        version.text = android.defaultConfig.versionName
    }
}

dependencies {
    implementation libs.kotlinx.coroutines
    implementation libs.kotlin.reflect
    implementation libs.recyclerview
    implementation libs.gson
    implementation libs.ffmpeg.kit
    implementation libs.osmdroid.android
    implementation libs.okhttp

    compileOnly files('libs/LSPosed-api-1.0-SNAPSHOT.jar')

    implementation project(':mapper')
}